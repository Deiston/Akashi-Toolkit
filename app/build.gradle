apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'

def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "moe.kcwiki.akashitoolkit"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode gitCommitCount
        versionName "0.13.5"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "BASE64_PUBLIC_KEY", "\"\""
        buildConfigField "String", "LEANCLOUD_APP_ID", "\"\""
        buildConfigField "String", "LEANCLOUD_APP_KEY", "\"\""

        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        googlePlayRelease {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationIdSuffix '.lite'
        }

        debug {
            applicationIdSuffix '.debug'
        }
    }

    productFlavors {
        leancloud {
        }

        firebase {
        }
    }

    sourceSets.leancloud {
        manifest.srcFile 'src/leancloud/AndroidManifest.xml'
    }

    sourceSets.firebase {
        manifest.srcFile 'src/firebase/AndroidManifest.xml'
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                String f = "${output.outputFile.parent}/akashitoolkit-${variant.versionCode}";

                if (variant.buildType.name.startsWith("googlePlay")) {
                    f += "-googleplay"
                } else if (variant.flavorName.equals("firebase")) {
                    f += "-firebase"
                }

                f += ".apk"

                output.outputFile = file(f)
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:gridlayout-v7:$supportLibraryVersion"
    compile "com.android.support:customtabs:$supportLibraryVersion"

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'

    compile 'com.google.code.gson:gson:2.8.2'

    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'com.squareup:otto:1.3.8'
    //compile 'com.github.RikkaW:DayNight-Theme:v1.0.13'
    compile (name:'daynightmode-release', ext:'aar')

    compile 'com.github.RikkaW:MaterialPreference:v1.4.4'
    compile 'com.github.RikkaW:MiniDrawer:v0.0.6'

    leancloudCompile 'cn.leancloud.android:avoscloud-sdk:v3.14.5'
    leancloudCompile 'cn.leancloud.android:avoscloud-push:v3.14.5@aar'
    leancloudCompile 'cn.leancloud.android:avoscloud-statistics:v3.14.5@aar'
    leancloudCompile 'cn.leancloud.android:Java-WebSocket:1.3.2-leancloud'
    leancloudCompile 'com.google.protobuf:protobuf-java:3.0.0'

    firebaseCompile "com.google.android.gms:play-services-base:$firebaseLibraryVersion"
    firebaseCompile "com.google.firebase:firebase-core:$firebaseLibraryVersion"
    firebaseCompile "com.google.firebase:firebase-messaging:$firebaseLibraryVersion"
    firebaseCompile "com.google.firebase:firebase-crash:$firebaseLibraryVersion"

    compile 'com.github.chrisbanes:PhotoView:1.2.6'

    //compile 'com.google.android.gms:play-services-analytics:9.6.1'

    compile 'io.reactivex:rxjava:1.2.3'
    compile 'io.reactivex:rxandroid:1.2.1'

    compile 'com.github.RikkaW:Markdown:-SNAPSHOT'

    compile 'com.github.rubensousa:gravitysnaphelper:0.1'
}